The [VALUES](https://docs.microsoft.com/dax/values-function-dax/?azure-portal=true) DAX function lets your formulas determine what values are in filter context. Its syntax is:

```dax
VALUES(<TableNameOrColumnName>)
```

The function requires passing in a table reference or a column reference. When you pass in a table reference, it returns a table object with the same columns and that contains rows for what's in filter context. When you pass in a column reference, it returns a single-column table of unique values that are in filter context.

The function always returns a table object and it's possible that the table contains multiple rows. So, to test whether a specific value is in filter context, your formula must first test that the VALUES function returns a single row. There are two functions to help you: The [HASONEVALUE](https://docs.microsoft.com/dax/hasonevalue-function-dax/?azure-portal=true) and the [SELECTEDVALUE](https://docs.microsoft.com/dax/selectedvalue-function/?azure-portal=true) DAX functions.

The HASONEVALUE function returns TRUE when a given column reference has been filtered down to a single value.

The SELECTEDVALUE function simplifies determining what a single value could be. When it's passed a column reference, it'll return a single value, or when there's more than one value in filter context, it'll return BLANK (or an alternate value you pass to the function).

Now, let's see how we can use the HASONEVALUE function. Add the following measure, which calculates sales commission. Note that at Adventure Works, the commission rate is 10% of revenue for all countries except the United States. In the United States, salespeople earn 15% commission. Format the measure as currency with two decimal places, and then add it to the table found on **Page 3** of the report.

```dax
Sales Commission =
[Revenue]
	* IF(
		HASONEVALUE('Sales Territory'[Country]),
		IF(
			VALUES('Sales Territory'[Country]) = "United States",
			0.15,
			0.1
		)
	)
```

> [!div class="mx-imgBorder"]
> [![An images shows a table visual with three columns: Region, Revenue, and Sales Commission. There are 10 region rows and a total. The total Sales Commission is BLANK.](../media/dax-table-region-sales-commission-1-ss.png)](../media/dax-table-region-sales-commission-1-ss.png#lightbox)

Notice that the total **Sales Commission** result is BLANK. It's because there are multiple values in filter context for the **Sales Territory** table **Country** column. In this case, the HASONEVALUE function returns FALSE, which results in the **Revenue** measure being multiplied by BLANK (a value multiplied by BLANK is BLANK). To produce a total, we need to use an iterator function. We'll see how to do that in the last unit of this module.

There are three other functions you can use to test filter state:

-   The [ISFITLERED](https://docs.microsoft.com/dax/isfiltered-function-dax/?azure-portal=true) DAX function returns TRUE when a passed in column reference is **directly** filtered.

-   The [ISCROSSFILTERED](https://docs.microsoft.com/dax/iscrossfiltered-function-dax/?azure-portal=true) DAX function returns TRUE when a passed in column reference is **indirectly** filtered. A column is cross-filtered when a filter applied to another column in the same table, or in a related table, affects the reference column by filtering it.

-   The [ISINSCOPE](https://docs.microsoft.com/dax/isinscope-function-dax/?azure-portal=true) DAX function returns TRUE when a passed in column reference is the level in a hierarchy of levels.

Let's now return to **Page 2** of the report, and then modify the **Revenue % Total Country** measure definition to test that the **Sales Territory** table **Region** column is in scope. If it's not in scope, the measure result should be BLANK.

```dax
Revenue % Total Country =
VAR CurrentRegionRevenue = [Revenue]
VAR TotalCountryRevenue =
	CALCULATE(
		[Revenue],
		REMOVEFILTERS('Sales Territory'[Region])
	)
RETURN
	IF(
		ISINSCOPE('Sales Territory'[Region]),
		DIVIDE(
			CurrentRegionRevenue,
			TotalCountryRevenue
		)
	)
```

> [!div class="mx-imgBorder"]
> [![An image shows a matrix visual titled Reseller Revenue has Group, Country, and Region grouped on the rows, and Revenue, Revenue % Total Region, Revenue % Total Country and Revenue % Total Group summarizations. There are BLANK values in the Revenue % Total Country summarizations.](../media/dax-matrix-sales-territory-revenue-5-ssm.png)](../media/dax-matrix-sales-territory-revenue-5-ssm.png#lightbox)

In the matrix visual, notice that **Revenue % Total Country** values are now only displayed when a region is in scope.
